*main

//--------------------------------------------------------------------------------------------------
[text text="用指令显示文字……"]
直接写文本也可以（但直接换行无效）
必须使用r指令换行[r]
l指令仅等待点击，不换行……[l]
用r指令配合l等待点击并换行[l][r]
p指令等待点击换页[p]
换页后自动清屏文字[r]
分别使用l等待点击，然后er文字清屏，相当于p指令。[l][er]
//--------------------------------------------------------------------------------------------------
//此处使用animate创建动画精灵，被textcursor使用，index会被回收
[animate index=1000 file="PageBreak_a" mode="horizontal" frame=16 interval=80 loop="bouncing"]
[textcursor index=1000 follow=true]
设定l和p等待时播放的指针动画textcursor[l][r]

[animate index=1000 file="PageBreak_a" mode="horizontal" frame=16 interval=10 loop="bouncing"]
[textcursor index=1000 follow=false pos=[100, 0]]
通过pos参数指定textcursor的绝对位置[l][r]

[animate index=1000 file="LineBreak_a" mode="horizontal" frame=16 interval=80 loop="bouncing"]
[textcursor index=1000 follow=true]
更换textcursor动画，并恢复自动follow的效果[l][r]
注意，使用click指令等待时，不会出现textcursor[click][er]
//--------------------------------------------------------------------------------------------------
一堆文字效果：[r][r]
[ruby text="头顶注释" size=8 char=2]
文字注释[r][r]
[i]斜体i[i][r]
[b]黑体b[b][r]
[s]删除线s[s][r]
[u]下划线u[u][r]
[click][er]
//--------------------------------------------------------------------------------------------------、
储存字体样式[l][storefont][r]
[textstyle shadow=true shadowcolor=#FF0000]带红色阴影了[l]
[restorefont]字体恢复了[l][r]
[textstyle stroke=true strokecolor=#FF0000]带红色描边了[l]
[restorefont]字体恢复了[l][r]
[textstyle size=30 color=#FF0000]红色大字符[l]
[restorefont]字体恢复了[l][r]
[textstyle bold=true under=true]黑体下划线[l]
[restorefont]字体恢复了[l][r]
[textstyle italic=true strike=true]斜体删除线[l]
[restorefont]字体恢复了[l][r]
[textstyle size=48 color=#FF0000 stroke=true strokecolor=#00FF00 shadow=true shadowcolor=#0000FF italic=true bold=true under=true strike=true]
全套文字效果[l]
[restorefont]字体恢复了[l][r]
如果前一行字太大，会占用下一行位置[p]
//--------------------------------------------------------------------------------------------------
[sprite index=5000 file="BG01a"][addto target=basic_layer index=5000]
增加背景图片用于对比文字窗口效果[p]

[textwindow color=#800000]
默认文字窗口颜色修改[p]

[textwindow color=#800000 opacity=196]
修改透明度为196[p]

[textwindow color=#800000 pos=[100,100] ]
修改文字窗口pos[p]

[textwindow color=#800000 rect=[20,100,300,400] ]
修改文字窗口rect[p]
换行会受到文字窗口的影响，窗口变窄会导致提前换行。[p]

[textwindow color=#800000 rect=[20,100,300,400] xinterval=20 yinterval=30]
修改文字x,y间距[p]

//此处插入单行脚本用来获取屏幕大小
#var scrW=System.resolutionSize[0];
#var scrH=System.resolutionSize[1];
[remove index=5000 delete=true]
[textwindow color=#000000 opacity=0 rect=[20,20,scrW-40,scrH-40] pos=[0,0] xinterval=0 yinterval=12]
恢复默认值，目前消除背景只能靠opacity=0[p]
//--------------------------------------------------------------------------------------------------
[locate x=100 y=100]locate改变文字坐标到(100，100)[l][r]
[locate x=20 y=20]回到系统默认(20，20)[p]
//--------------------------------------------------------------------------------------------------
[messagelayer index=9999 active=true][addto index=9999 target=basic_layer]
新建9999号消息层，显示文字，该层没有textcursor[r]
必须addto到有效的target，例如basic_layer[l][r]
[messagelayer index=9999 active=false]
在当前active的layer上执行active=false，什么都不会发生。[l][r]
[textwindow color=#800000 pos=[100,100] ]
修改当前消息层位置和背景[l][r]
[textstyle color=#00FF00]
修改当前消息层的字体颜色[l][r]
[messagelayer index=message_layer active=true]
回到默认消息层，显示文字[l][r]
[messagelayer index=9999 active=true]
再次回到9999，点击换页[p]
[messagelayer index=message_layer active=true]
再次回到默认消息层[l][r]
[remove index=9999 delete=true]
删除了9999号消息层[p]
//--------------------------------------------------------------------------------------------------
[textspeed interval=0]
修改文字速度为最慢，这段文字显示的非常慢。[p]
[textspeed interval=100]
修改文字速度为最快，这段文字显示的非常快。[p]
[textspeed interval=50]
恢复默认速度50，这段文字显示正常。[p]
//--------------------------------------------------------------------------------------------------
测试文字显示隐藏textoff和texton[l][r]
3秒钟的文字fadeout，点击后是3秒钟的文字fadein。
[textoff time=3000][click]
[texton time=3000][click]
文字隐显效果结束，本质是文字窗口的Action[p]
//--------------------------------------------------------------------------------------------------
演示结束[l]